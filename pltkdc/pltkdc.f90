!######################################################################
! pltkdc: a program to plot the potentials of the model Hamiltonian
!         generated by the KDC program
!######################################################################

program pltkdc

  implicit none

!----------------------------------------------------------------------
! Read the command line arguments
!----------------------------------------------------------------------
  call rdinp_pltkdc

!----------------------------------------------------------------------
! Read the parameter binary file
!----------------------------------------------------------------------
  call rdbinfile

!----------------------------------------------------------------------
! Calculate the surfaces
!----------------------------------------------------------------------
  call calcsurf

!----------------------------------------------------------------------
! Get the ab initio data
!----------------------------------------------------------------------
  call getabinit
  
!----------------------------------------------------------------------
! Write the gnuplot file and plot the surfaces to the screen
!----------------------------------------------------------------------
  call wrgnuplot
  
contains

!######################################################################
  
  subroutine rdinp_pltkdc

    use constants
    use channels
    use sysinfo
    use pltglobal
    
    implicit none

    integer            :: n
    character(len=120) :: string1,string2

!----------------------------------------------------------------------
! Set default values
!----------------------------------------------------------------------
    ! Parameter file name
    abin=''
    
    ! Plotting mode
    mplt=-1

    ! Initial and final states
    si=-1
    sf=-1

    ! Coordinate interval
    qi=-7.0d0
    qf=+7.0d0

    ! Energy interval
    ei=-999.0d0
    ef=-999.0d0

    ! No. points
    npnts=1000

    ! eps output
    leps=.false.

    ! Surface type: 1 <-> adiabatic potentials
    !               2 <-> diabatic potentials
    surftyp=1

!----------------------------------------------------------------------
! Exit if no arguments have been given
!----------------------------------------------------------------------
    if (iargc().eq.0) then
       write(6,'(/,2x,a,/)') 'No arguments have been given!'
       STOP
    endif

!----------------------------------------------------------------------
! Read the command line arguments
!----------------------------------------------------------------------
    n=0
5   continue
    
    n=n+1
    call getarg(n,string1)

    if (string1.eq.'-h') then
       call wrhelp
    else if (string1.eq.'-f') then
       n=n+1
       call getarg(n,string2)
       abin=string2
    else if (string1.eq.'-m') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) mplt
    else if (string1.eq.'-xrange') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) qi
       n=n+1
       call getarg(n,string2)
       read(string2,*) qf
    else if (string1.eq.'-yrange') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) ei
       n=n+1
       call getarg(n,string2)
       read(string2,*) ef
    else if (string1.eq.'-npnts') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) npnts
    else if (string1.eq.'-si') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) si
    else if (string1.eq.'-sf') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) sf
    else if (string1.eq.'-eps') then
       leps=.true.
    else if (string1.eq.'-adiab') then
       surftyp=1
    else if (string1.eq.'-diab') then
       surftyp=2
    else
       write(6,'(/,2x,a,/)') 'Unknown keyword: '//trim(string1)
       stop
    endif

    if (n.lt.iargc()) goto 5

!----------------------------------------------------------------------
! Make sure that all the required information has been given
!----------------------------------------------------------------------
    if (mplt.eq.-1) then
       write(6,'(/,2x,a,/)') 'The plotting mode has not been given'
       stop
    endif

    if (abin.eq.'') then
       write(6,'(/,2x,a,/)') 'The parameter file name has not been &
            given'
       stop
    endif

    return
    
  end subroutine rdinp_pltkdc

!######################################################################

    subroutine wrhelp

    implicit none

    integer :: i

!----------------------------------------------------------------------
! Write the input options to screen, then quit
!----------------------------------------------------------------------
    ! Purpose
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)') 'Purpose'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') 'Plots the model potentials of the model &
         Hamiltonian constructed using the KDC program'

    ! Usage
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)') 'Usage'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') 'pltkdc -f -m (-xrange -yrange -npnts -si -sf &
         -eps -diab -adiab)'

    ! Options
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)')   'Options'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') '-f F              : &
         The Hamiltonian parameters are written to the file F'
    write(6,'(a)') '-m M              : &
         The plot is along the Mth mode'
    write(6,'(a)') '-xrange QI QF     : &
         The plot is over the coordinate interval [QI,QF]'
    write(6,'(a)') '-yrange EI EF     : &
         The plot is over the energy interval [EI,EF]'
    write(6,'(a)') '-npnts N          : &
         The plot will be made using N points for each state'
    write(6,'(a)') '-si I             : &
         I is the index of the lowest state potential to be plotted'
    write(6,'(a)') '-sf F             : &
         F is the index of the highest state potential to be plotted'
    write(6,'(a)') '-eps              : &
         Also write the model potentials to an eps file'
    write(6,'(a)') '-diab             : &
         Plot the diabatic potentials (default)'
    write(6,'(a)') '-adiab            : &
         Plot the adiabatic potentials'
    
    write(6,'(/)')
    STOP
    
    return
    
  end subroutine wrhelp
  
!######################################################################

  subroutine rdbinfile

    use constants
    use channels
    use iomod
    use sysinfo
    use pltglobal
    use symmetry
    use parameters
    
    implicit none

!----------------------------------------------------------------------
! Open the parameter file
!----------------------------------------------------------------------
    call freeunit(ibin)
    open(ibin,file=abin,form='unformatted',status='old')
    
!----------------------------------------------------------------------
! System dimensions
!----------------------------------------------------------------------
    read(ibin) nmodes
    read(ibin) nsta

!----------------------------------------------------------------------
! No. ab initio diabatic potential values
!----------------------------------------------------------------------
    read(ibin) ndat
    
!----------------------------------------------------------------------
! Allocate arrays
!----------------------------------------------------------------------
    allocate(e0(nsta))
    allocate(freq(nmodes))
    allocate(kappa(nmodes,nsta))
    allocate(lambda(nmodes,nsta,nsta))
    allocate(gamma(nmodes,nmodes,nsta))
    allocate(mu(nmodes,nmodes,nsta,nsta))
    allocate(iota(nmodes,nsta))
    allocate(tau(nmodes,nsta,nsta))
    allocate(epsilon(nmodes,nsta))
    allocate(xi(nmodes,nsta,nsta))
    allocate(kappa_mask(nmodes,nsta))
    allocate(lambda_mask(nmodes,nsta,nsta))
    allocate(gamma_mask(nmodes,nmodes,nsta))
    allocate(mu_mask(nmodes,nmodes,nsta,nsta))
    allocate(iota_mask(nmodes,nsta))
    allocate(tau_mask(nmodes,nsta,nsta))
    allocate(epsilon_mask(nmodes,nsta))
    allocate(xi_mask(nmodes,nsta,nsta))
    allocate(wq0(nsta))
    allocate(wdisp(nsta,nsta,ndat))
    allocate(qvec(nmodes,ndat))
    
!----------------------------------------------------------------------
! Vertical excitation energies
!----------------------------------------------------------------------
    read(ibin) e0
    
!----------------------------------------------------------------------
! Frequencies
!----------------------------------------------------------------------
    read(ibin) freq

!----------------------------------------------------------------------
! Coupling coefficients
!----------------------------------------------------------------------
    read(ibin) kappa
    read(ibin) lambda
    read(ibin) gamma
    read(ibin) mu
    read(ibin) iota
    read(ibin) tau
    read(ibin) epsilon
    read(ibin) xi
    
!----------------------------------------------------------------------
! Masks
!----------------------------------------------------------------------
    read(ibin) kappa_mask
    read(ibin) lambda_mask
    read(ibin) gamma_mask
    read(ibin) mu_mask
    read(ibin) iota_mask
    read(ibin) tau_mask
    read(ibin) epsilon_mask
    read(ibin) xi_mask

!----------------------------------------------------------------------
! Ab inito diabatic potential values
!----------------------------------------------------------------------
    read(ibin) wq0
    read(ibin) wdisp
    read(ibin) qvec
    
!----------------------------------------------------------------------
! Close the parameter file
!----------------------------------------------------------------------
    close(ibin)
    
    return
    
  end subroutine rdbinfile

!######################################################################

  subroutine calcsurf

    use constants
    use iomod
    use sysinfo
    use symmetry
    use pltglobal

    implicit none

    integer                     :: i
    real(dp)                    :: dq
    real(dp), dimension(nmodes) :: q

!----------------------------------------------------------------------
! Allocate and initialise arrays    
!----------------------------------------------------------------------
    ! Surface to be plotted
    allocate(surf(npnts,nsta))
    surf=0.0d0
    
!----------------------------------------------------------------------
! Calculate the model potentials
!----------------------------------------------------------------------
    ! Step size
    dq=(qf-qi)/(npnts-1)

    q=0.0d0

    ! Loop over points
    do i=1,npnts
       
       ! Current geometry
       q(mplt)=qi+(i-1)*dq

       ! Current set of energies
       surf(i,:)=surface(q)

    enddo

    return
    
  end subroutine calcsurf
    
!######################################################################

    function surface(q) result(func)

    use constants
    use sysinfo
    use potfuncs
    use pltglobal

    implicit none

    integer                        :: s
    real(dp), dimension(nmodes)    :: q
    real(dp), dimension(nsta)      :: func
    real(dp), dimension(nsta,nsta) :: wmat
    
    select case(surftyp)

    case(1) ! adiabatic potentials
       func=adiabaticpot(q)

    case(2) ! diabatic potentials
       wmat=diabaticpot(q)
       do s=1,nsta
          func(s)=wmat(s,s)
       enddo
          
    end select

    return

  end function surface

!######################################################################

  subroutine getabinit

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer                        :: m,n,ndisp,mindx,k,s,s1,s2
    integer                        :: e2,error
    real(dp), parameter            :: thrsh=1e-4_dp
    real(dp), dimension(nsta)      :: v
    real(dp), dimension(nsta,nsta) :: w
    real(dp), dimension(3*nsta)    :: work
    
!----------------------------------------------------------------------
! Determine the no. ab initio points for the requested cut
!----------------------------------------------------------------------
    nabinit=0
    
    ! Loop over geometries
    do n=1,ndat

       ! Determine the no. displaced modes
       ndisp=0.0d0
       do m=1,nmodes
          if (abs(qvec(m,n)).gt.thrsh) then
             ndisp=ndisp+1
             mindx=m
          endif
       enddo

       ! Update nabinit if only the plotting mode is displaced
       if (ndisp.eq.1.and.mindx.eq.mplt) nabinit=nabinit+1
       
    enddo

!----------------------------------------------------------------------
! Allocate arrays
!----------------------------------------------------------------------
    allocate(iabinit(nabinit))
    iabinit=0

    allocate(abinit(nsta,nsta,nabinit))
    abinit=0.0d0
    
!----------------------------------------------------------------------
! Fill in the ab initio potential index array
!----------------------------------------------------------------------
    ! Displaced geometries
    k=0
    ! Loop over geometries
    do n=1,ndat

       ! Determine the no. displaced modes
       ndisp=0.0d0
       do m=1,nmodes
          if (abs(qvec(m,n)).gt.thrsh) then
             ndisp=ndisp+1
             mindx=m
          endif
       enddo

       ! Fill in abinit if only the plotting mode is displaced
       if (ndisp.eq.1.and.mindx.eq.mplt) then
          k=k+1
          iabinit(k)=n
       endif

    enddo

!----------------------------------------------------------------------
! Fill in the ab initio potential array
!----------------------------------------------------------------------
    do n=1,nabinit

       ! Adiabatic potentials
       if (surftyp.eq.1) then
          e2=3*nsta
          w=wdisp(:,:,iabinit(n))
          call dsyev('V','U',nsta,w,nsta,v,work,e2,error)
          do s=1,nsta
             abinit(s,s,n)=e0(s)+(v(s)-wq0(s))*eh2ev
          enddo
       endif

       ! Diabatic potentials
       if (surftyp.eq.2) then
          do s=1,nsta
             abinit(s,s,n)=e0(s)+(wdisp(s,s,iabinit(n))-wq0(s))*eh2ev
          enddo
          do s1=1,nsta-1
             do s2=s1+1,nsta
                abinit(s1,s2,n)=wdisp(s1,s2,iabinit(n))*eh2ev
                abinit(s2,s1,n)=abinit(s1,s2,n)
             enddo
          enddo
       endif
       
    enddo
    

    
    return
    
  end subroutine getabinit
  
!######################################################################

  subroutine wrgnuplot

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer            :: unit,s,i
    character(len=2)   :: am,as
    character(len=220) :: filename,datfile,string
    
!----------------------------------------------------------------------
! Filenames
!----------------------------------------------------------------------
    write(am,'(i2)') mplt

    ! data file
    datfile='plot_q'//trim(adjustl(am))//'.dat'

    ! gnuplot file
    filename='plot_q'//trim(adjustl(am))//'.gnu'

!----------------------------------------------------------------------
! Energy ranges and states
!----------------------------------------------------------------------
    if (si.eq.-1) si=1
    if (sf.eq.-1) sf=nsta
    if (ei.eq.-999.0d0) ei=min(-0.2,0.98d0*minval(surf(:,si:sf)))
    if (ef.eq.-999.0d0) ef=1.02d0*maxval(surf(:,si:sf))

!----------------------------------------------------------------------
! Open the gnuplot file
!----------------------------------------------------------------------
    call freeunit(unit)
    open(unit,file=filename,form='formatted',status='unknown')

!----------------------------------------------------------------------
! Write the gnuplot file
!----------------------------------------------------------------------
    ! Set up
    write(unit,'(a)') 'set size square'
    write(unit,'(a)') 'unset key'
!    write(unit,'(a)') 'monitorSize=system("xrandr | awk &
!         ''/\*/{sub(/x/,\",\");print $1;exit}''")'
!    write(unit,'(a,/)') 'set terminal x11 size @monitorSize'

    ! Axis labels
    write(unit,'(a)') 'set ylabel ''Energy (eV)'''
    write(am,'(i2)') mplt
    string='set xlabel ''Q_{'//trim(adjustl(am))//'}'''
    write(unit,'(a,/)') trim(string)

    ! Ranges
    write(unit,'(2(a,F6.2),a)') 'set xrange [',qi,':',qf,']'
    write(unit,'(2(a,F6.2),a,/)') 'set yrange [',ei,':',ef,']'
    
    ! eps output
    if (leps) then
       write(unit,'(/,a)') 'set terminal postscript eps &
            enhanced color solid "Helvetica" 20'
       write(unit,'(a)') 'set output '''&
            //filename(1:index(filename,'.gnu'))//'eps'''
    endif

    ! Plot command
    string='plot '
    do s=si,sf
       string=trim(string)//' "-" w l lw 4,'
    enddo   
    do s=si,sf
       string=trim(string)//' "-" w p pt 7 ps 1.5 lt 8'
       if (s.ne.sf) string=trim(string)//','
    enddo
    write(unit,'(a)') trim(string)
    
    ! Model potentials
    write(unit,'(/)')
    do s=si,sf
       do i=1,npnts
          write(unit,*) qi+(i-1)*(qf-qi)/(npnts-1),surf(i,s)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo

    ! Ab inito data
    do s=si,sf
       write(unit,*) 0.0d0,e0(s)
       do i=1,nabinit
          write(unit,*) qvec(mplt,iabinit(i)),abinit(s,s,i)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo
    
    ! pause -1
    if (.not.leps) write(unit,'(/,a)') 'pause -1'
    
!----------------------------------------------------------------------
! Close the gnuplot file
!----------------------------------------------------------------------
    close(unit)

!----------------------------------------------------------------------
! Plot the surfaces to the screen
!----------------------------------------------------------------------
    call system('gnuplot '//trim(filename))
    
    return
    
  end subroutine wrgnuplot
    
!######################################################################
  
end program pltkdc
