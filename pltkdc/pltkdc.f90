!######################################################################
! pltkdc: a program to plot the potentials of the model Hamiltonian
!         generated by the KDC program
!######################################################################

program pltkdc

  implicit none

!----------------------------------------------------------------------
! Read the command line arguments
!----------------------------------------------------------------------
  call rdinp_pltkdc

!----------------------------------------------------------------------
! Read the parameter binary file
!----------------------------------------------------------------------
  call rdbinfile

!----------------------------------------------------------------------
! Calculate the surfaces
!----------------------------------------------------------------------
  call calcsurf

!----------------------------------------------------------------------
! Get the ab initio data
!----------------------------------------------------------------------
  call getabinit
  
!----------------------------------------------------------------------
! Write the gnuplot file and plot the surfaces to the screen
!----------------------------------------------------------------------
  call wrgnuplot
  
contains

!######################################################################
  
  subroutine rdinp_pltkdc

    use constants
    use channels
    use sysinfo
    use pltglobal
    
    implicit none

    integer            :: n
    character(len=120) :: string1,string2

!----------------------------------------------------------------------
! Set default values
!----------------------------------------------------------------------
    ! Parameter file name
    abin=''
    
    ! Plotting modes
    mplt=-1
    mplt2=-1

    ! Diagonal cuts
    ldiagcut=.false.
    
    ! Initial and final states
    si=-1
    sf=-1

    ! Coordinate interval
    qi=-7.0d0
    qf=+7.0d0

    ! Energy/function value interval
    ei=-999.0d0
    ef=-999.0d0

    ! No. points
    npnts=1000

    ! eps output
    leps=.false.

    ! Surface type: 1 <-> adiabatic potentials
    !               2 <-> diabatic potentials
    !               3 <-> diabatic dipoles/transition dipoles
    !               4 <-> diabatic couplings
    surftyp=1

!----------------------------------------------------------------------
! Exit if no arguments have been given
!----------------------------------------------------------------------
    if (iargc().eq.0) then
       write(6,'(/,2x,a,/)') 'No arguments have been given!'
       STOP
    endif

!----------------------------------------------------------------------
! Read the command line arguments
!----------------------------------------------------------------------
    n=0
5   continue
    
    n=n+1
    call getarg(n,string1)

    if (string1.eq.'-h') then
       call wrhelp
    else if (string1.eq.'-f') then
       n=n+1
       call getarg(n,string2)
       abin=string2
    else if (string1.eq.'-m') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) mplt
    else if (string1.eq.'-m2') then
       ldiagcut=.true.
       n=n+1
       call getarg(n,string2)
       read(string2,*) mplt2
    else if (string1.eq.'-xrange') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) qi
       n=n+1
       call getarg(n,string2)
       read(string2,*) qf
    else if (string1.eq.'-yrange') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) ei
       n=n+1
       call getarg(n,string2)
       read(string2,*) ef
    else if (string1.eq.'-npnts') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) npnts
    else if (string1.eq.'-si') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) si
    else if (string1.eq.'-sf') then
       n=n+1
       call getarg(n,string2)
       read(string2,*) sf
    else if (string1.eq.'-eps') then
       leps=.true.
    else if (string1.eq.'-adiab') then
       surftyp=1
    else if (string1.eq.'-diab') then
       surftyp=2
    else if (string1.eq.'-dip') then
       surftyp=3
       n=n+1
       call getarg(n,string2)
       read(string2,*) dipsta1
       n=n+1
       call getarg(n,string2)
       read(string2,*) dipsta2
    else if (string1.eq.'-diabcp') then
       surftyp=4
       n=n+1
       call getarg(n,string2)
       read(string2,*) dcpsta1
       n=n+1
       call getarg(n,string2)
       read(string2,*) dcpsta2
    else
       write(6,'(/,2x,a,/)') 'Unknown keyword: '//trim(string1)
       stop
    endif

    if (n.lt.iargc()) goto 5

!----------------------------------------------------------------------
! Make sure that all the required information has been given
!----------------------------------------------------------------------
    if (mplt.eq.-1) then
       write(6,'(/,2x,a,/)') 'The plotting mode has not been given'
       stop
    endif

    if (abin.eq.'') then
       write(6,'(/,2x,a,/)') 'The parameter file name has not been &
            given'
       stop
    endif

    return
    
  end subroutine rdinp_pltkdc

!######################################################################

    subroutine wrhelp

    implicit none

    integer :: i

!----------------------------------------------------------------------
! Write the input options to screen, then quit
!----------------------------------------------------------------------
    ! Purpose
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)') 'Purpose'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') 'Plots the model potentials of the model &
         Hamiltonian constructed using the KDC program'

    ! Usage
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)') 'Usage'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') 'pltkdc -f -m (-m2 -xrange -yrange -npnts -si -sf &
         -eps -diab -adiab)'

    ! Options
    write(6,'(/,25a)') ('-',i=1,25)
    write(6,'(a)')   'Options'
    write(6,'(25a)') ('-',i=1,25)
    write(6,'(a)') '-f F              : &
         The Hamiltonian parameters are written to the file F'
    write(6,'(a)') '-m M              : &
         The plot is along the Mth mode'
    write(6,'(a)') '-m2 M2            : &
         The plot is along the diagonal of the Mth and M2th modes'
    write(6,'(a)') '-xrange QI QF     : &
         The plot is over the coordinate interval [QI,QF]'
    write(6,'(a)') '-yrange EI EF     : &
         The plot is over the energy interval [EI,EF]'
    write(6,'(a)') '-npnts N          : &
         The plot will be made using N points for each state'
    write(6,'(a)') '-si I             : &
         I is the index of the lowest state potential to be plotted'
    write(6,'(a)') '-sf F             : &
         F is the index of the highest state potential to be plotted'
    write(6,'(a)') '-eps              : &
         Also write the model potentials to an eps file'
    write(6,'(a)') '-diab             : &
         Plot the diabatic potentials'
    write(6,'(a)') '-adiab            : &
         Plot the adiabatic potentials (default)'
    
    write(6,'(/)')
    stop
    
    return
    
  end subroutine wrhelp
  
!######################################################################

  subroutine rdbinfile

    use constants
    use channels
    use iomod
    use sysinfo
    use pltglobal
    use symmetry
    use parameters
    
    implicit none

!----------------------------------------------------------------------
! Open the parameter file
!----------------------------------------------------------------------
    call freeunit(ibin)
    open(ibin,file=abin,form='unformatted',status='old')
    
!----------------------------------------------------------------------
! System dimensions
!----------------------------------------------------------------------
    read(ibin) nmodes
    read(ibin) nsta

!----------------------------------------------------------------------
! No. ab initio diabatic potential values
!----------------------------------------------------------------------
    read(ibin) ndat

!----------------------------------------------------------------------
! Diabatic dipole flag
!----------------------------------------------------------------------
    read(ibin) ldip
    
!----------------------------------------------------------------------
! Allocate arrays
!----------------------------------------------------------------------
    ! Model diabatic potential arrays
    allocate(e0(nsta))
    allocate(freq(nmodes))
    allocate(kappa(nmodes,nsta))
    allocate(lambda(nmodes,nsta,nsta))
    allocate(gamma(nmodes,nmodes,nsta))
    allocate(mu(nmodes,nmodes,nsta,nsta))
    allocate(iota(nmodes,nsta))
    allocate(tau(nmodes,nsta,nsta))
    allocate(epsilon(nmodes,nsta))
    allocate(xi(nmodes,nsta,nsta))
    allocate(kappa_mask(nmodes,nsta))
    allocate(lambda_mask(nmodes,nsta,nsta))
    allocate(gamma_mask(nmodes,nmodes,nsta))
    allocate(mu_mask(nmodes,nmodes,nsta,nsta))
    allocate(iota_mask(nmodes,nsta))
    allocate(tau_mask(nmodes,nsta,nsta))
    allocate(epsilon_mask(nmodes,nsta))
    allocate(xi_mask(nmodes,nsta,nsta))

    ! Ab initio diabatic potential arrays
    allocate(wq0(nsta))
    allocate(wdisp(nsta,nsta,ndat))
    allocate(qvec(nmodes,ndat))

    ! Diabatic dipole arrays
    if (ldip) then
       ! Coefficients
       allocate(dip0(nsta,nsta,3))
       allocate(dip1(nmodes,nsta,nsta,3))
       allocate(dip2(nmodes,nmodes,nsta,nsta,3))
       allocate(dip3(nmodes,nsta,nsta,3))
       allocate(dip4(nmodes,nsta,nsta,3))
       ! Masks
       allocate(dip1_mask(nmodes,nsta,nsta,3))
       allocate(dip2_mask(nmodes,nmodes,nsta,nsta,3))
       allocate(dip3_mask(nmodes,nsta,nsta,3))
       allocate(dip4_mask(nmodes,nsta,nsta,3))
       ! Ab initio dipole values
       allocate(ddisp(nsta,nsta,3,ndat))
    endif
    
!----------------------------------------------------------------------
! Vertical excitation energies
!----------------------------------------------------------------------
    read(ibin) e0
    
!----------------------------------------------------------------------
! Frequencies
!----------------------------------------------------------------------
    read(ibin) freq

!----------------------------------------------------------------------
! Coupling coefficients
!----------------------------------------------------------------------
    read(ibin) kappa
    read(ibin) lambda
    read(ibin) gamma
    read(ibin) mu
    read(ibin) iota
    read(ibin) tau
    read(ibin) epsilon
    read(ibin) xi
    
!----------------------------------------------------------------------
! Masks
!----------------------------------------------------------------------
    read(ibin) kappa_mask
    read(ibin) lambda_mask
    read(ibin) gamma_mask
    read(ibin) mu_mask
    read(ibin) iota_mask
    read(ibin) tau_mask
    read(ibin) epsilon_mask
    read(ibin) xi_mask

!----------------------------------------------------------------------
! Ab inito diabatic potential values
!----------------------------------------------------------------------
    read(ibin) wq0
    read(ibin) wdisp
    read(ibin) qvec

!----------------------------------------------------------------------
! Diabatic dipole surfaces
!----------------------------------------------------------------------
    if (ldip) then

       ! Coefficients
       read(ibin) dip0
       read(ibin) dip1
       read(ibin) dip2
       read(ibin) dip3
       read(ibin) dip4

       ! Masks
       read(ibin) dip1_mask
       read(ibin) dip2_mask
       read(ibin) dip3_mask
       read(ibin) dip4_mask

       ! Ab initio diabatic dipoles
       read(ibin) ddisp
       
    endif
    
!----------------------------------------------------------------------
! Close the parameter file
!----------------------------------------------------------------------
    close(ibin)
    
    return
    
  end subroutine rdbinfile

!######################################################################

  subroutine calcsurf

    use constants
    use iomod
    use sysinfo
    use symmetry
    use pltglobal

    implicit none

    integer                     :: i
    real(dp)                    :: dq
    real(dp), dimension(nmodes) :: q

!----------------------------------------------------------------------
! Allocate and initialise arrays    
!----------------------------------------------------------------------
    ! Surface to be plotted
    allocate(surf(npnts,nsta))
    surf=0.0d0
    
!----------------------------------------------------------------------
! Calculate the model potentials
!----------------------------------------------------------------------
    ! Step size
    dq=(qf-qi)/(npnts-1)

    q=0.0d0

    ! Loop over points
    do i=1,npnts
       
       ! Current geometry
       q(mplt)=qi+(i-1)*dq
       if (ldiagcut) q(mplt2)=qi+(i-1)*dq
       
       ! Current set of energies
       surf(i,:)=surface(q)

    enddo

    return
    
  end subroutine calcsurf
    
!######################################################################

    function surface(q) result(func)

    use constants
    use sysinfo
    use potfuncs
    use pltglobal
    
    implicit none

    integer                        :: s
    real(dp), dimension(nmodes)    :: q
    real(dp), dimension(nsta)      :: func
    real(dp), dimension(nsta,nsta) :: wmat
    real(dp), dimension(3)         :: dip

    select case(surftyp)

    case(1) ! adiabatic potentials
       func=adiabaticpot(q)

    case(2) ! diabatic potentials
       wmat=diabaticpot(q)
       do s=1,nsta
          func(s)=wmat(s,s)
       enddo

    case(3) ! diabatic dipoles/transition dipoles
       func(1:3)=diabdip(q,dipsta1,dipsta2)

    case(4) ! diabatic couplings
       wmat=diabaticpot(q)
       func(1)=wmat(dcpsta1,dcpsta2)
       
    end select

    return

  end function surface

!######################################################################

  subroutine getabinit

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer                        :: m,n,ndisp,k,s,s1,s2,c,dim
    integer, dimension(2)          :: mindx
    integer                        :: e2,error
    real(dp), parameter            :: thrsh=1e-4_dp
    real(dp), dimension(nsta)      :: v
    real(dp), dimension(nsta,nsta) :: w
    real(dp), dimension(3*nsta)    :: work
    
!----------------------------------------------------------------------
! Determine the no. ab initio points for the requested cut
!----------------------------------------------------------------------
    nabinit=0
    
    ! Loop over geometries
    do n=1,ndat

       ! Determine the no. displaced modes
       ndisp=0.0d0
       do m=1,nmodes
          if (abs(qvec(m,n)).gt.thrsh) then
             ndisp=ndisp+1
             mindx(ndisp)=m
          endif
       enddo

       ! Update nabinit if only the plotting mode is displaced
       if (ldiagcut) then
          if (ndisp.eq.2.and.mindx(1).eq.mplt.and.mindx(2).eq.mplt2) &
               nabinit=nabinit+1
       else
          if (ndisp.eq.1.and.mindx(1).eq.mplt) nabinit=nabinit+1
       endif
          
    enddo

!----------------------------------------------------------------------
! Allocate arrays
!----------------------------------------------------------------------
    allocate(iabinit(nabinit))
    iabinit=0

    dim=max(3,nsta)
    allocate(abinit(dim,dim,nabinit))
    abinit=0.0d0
    
!----------------------------------------------------------------------
! Fill in the ab initio potential index array
!----------------------------------------------------------------------
    ! Displaced geometries
    k=0
    ! Loop over geometries
    do n=1,ndat

       ! Determine the no. displaced modes
       ndisp=0.0d0
       do m=1,nmodes
          if (abs(qvec(m,n)).gt.thrsh) then
             ndisp=ndisp+1
             mindx(ndisp)=m
          endif
       enddo

       ! Fill in abinit if only the plotting mode is displaced
       if (ldiagcut) then
          if (ndisp.eq.2.and.mindx(1).eq.mplt.and.mindx(2).eq.mplt2) then
             k=k+1
             iabinit(k)=n
          endif
       else
          if (ndisp.eq.1.and.mindx(1).eq.mplt) then
             k=k+1
             iabinit(k)=n
          endif
       endif
       
    enddo

!----------------------------------------------------------------------
! Fill in the ab initio potential array
!----------------------------------------------------------------------
    do n=1,nabinit

       ! Adiabatic potentials
       if (surftyp.eq.1) then
          e2=3*nsta
          w=wdisp(:,:,iabinit(n))
          call dsyev('V','U',nsta,w,nsta,v,work,e2,error)
          do s=1,nsta
             abinit(s,s,n)=e0(s)+(v(s)-wq0(s))*eh2ev
          enddo
       endif

       ! Diabatic potentials and diabatic couplings
       if (surftyp.eq.2.or.surftyp.eq.4) then
          do s=1,nsta
             abinit(s,s,n)=e0(s)+(wdisp(s,s,iabinit(n))-wq0(s))*eh2ev
          enddo
          do s1=1,nsta-1
             do s2=s1+1,nsta
                abinit(s1,s2,n)=wdisp(s1,s2,iabinit(n))*eh2ev
                abinit(s2,s1,n)=abinit(s1,s2,n)
             enddo
          enddo
       endif

       ! Diabatic dipoles
       if (surftyp.eq.3) then
          do c=1,3
             abinit(c,c,n)=ddisp(dipsta1,dipsta2,c,iabinit(n))
          enddo
       endif
       
    enddo
    
    return
    
  end subroutine getabinit
  
!######################################################################

  subroutine wrgnuplot

    use pltglobal
    
    implicit none

    if (surftyp.eq.1.or.surftyp.eq.2) then
       ! Adiabatic or diabatic potentials
       call wrgnuplot_potentials
    else if (surftyp.eq.3) then
       ! Diabatic dipoles
       call wrgnuplot_dipoles
    else if (surftyp.eq.4) then
       ! Diabatic couplings
       call wrgnuplot_diabcp
    endif
    
    return
    
  end subroutine wrgnuplot

!######################################################################

  subroutine wrgnuplot_potentials

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer             :: unit,s,i,k
    character(len=4)    :: am,am2
    character(len=220)  :: filename
    character(len=1200) :: string

!----------------------------------------------------------------------
! Filename
!----------------------------------------------------------------------
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    
    ! gnuplot file
    if (ldiagcut) then
       filename='plot_q'//trim(adjustl(am))//'_q'&
            //trim(adjustl(am2))//'.gnu'
    else
       filename='plot_q'//trim(adjustl(am))//'.gnu'
    endif
       
!----------------------------------------------------------------------
! Open the gnuplot file
!----------------------------------------------------------------------
    call freeunit(unit)
    open(unit,file=filename,form='formatted',status='unknown')

!----------------------------------------------------------------------
! Energy ranges and states
!----------------------------------------------------------------------
    if (si.eq.-1) si=1
    if (sf.eq.-1) sf=nsta
    if (ei.eq.-999.0d0) ei=min(-0.2,0.98d0*minval(surf(:,si:sf)))
    if (ef.eq.-999.0d0) ef=1.02d0*maxval(surf(:,si:sf))
    
!----------------------------------------------------------------------
! Write the gnuplot file
!----------------------------------------------------------------------
    ! Set up
    write(unit,'(a)') 'set size square'
    write(unit,'(a)') 'unset key'
    write(unit,'(a)') 'monitorSize=system("xrandr | awk &
         ''/\*/{sub(/x/,\",\");print $1;exit}''")'
    !write(unit,'(a,/)') 'set terminal x11 size @monitorSize'

    ! Axis labels
    write(unit,'(a)') 'set ylabel ''Energy (eV)'''
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    string='set xlabel ''Q_{'//trim(adjustl(am))//'}'''
    if (ldiagcut) then
       k=len_trim(string)-1
       string=string(1:k)//' Q_{'//trim(adjustl(am2))//'}'''
    endif
    write(unit,'(a,/)') trim(string)
    
    ! Ranges
    write(unit,'(2(a,F6.2),a)') 'set xrange [',qi,':',qf,']'
    write(unit,'(2(a,F6.2),a,/)') 'set yrange [',ei,':',ef,']'
    
    ! eps output
    if (leps) then
       write(unit,'(/,a)') 'set terminal postscript eps &
            enhanced color solid "Helvetica" 20'
       write(unit,'(a)') 'set output '''&
            //filename(1:index(filename,'.gnu'))//'eps'''
    endif

    ! Plot command
    string='plot '
    do s=si,sf
       string=trim(string)//' "-" w l lw 4,'
    enddo
    do s=si,sf
       string=trim(string)//' "-" w p pt 7 ps 0.7 lt 8'
       if (s.ne.sf) string=trim(string)//','
    enddo
    write(unit,'(a)') trim(string)
    
    ! Model potentials
    write(unit,'(/)')
    do s=si,sf
       do i=1,npnts
          write(unit,*) qi+(i-1)*(qf-qi)/(npnts-1),surf(i,s)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo

    ! Ab inito data
    do s=si,sf
       write(unit,*) 0.0d0,e0(s)
       do i=1,nabinit
          write(unit,*) qvec(mplt,iabinit(i)),abinit(s,s,i)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo
    
    ! pause -1
    if (.not.leps) write(unit,'(/,a)') 'pause -1'
    
!----------------------------------------------------------------------
! Close the gnuplot file
!----------------------------------------------------------------------
    close(unit)

!----------------------------------------------------------------------
! Plot the surfaces to the screen
!----------------------------------------------------------------------
    call system('gnuplot '//trim(filename))
    
    return
    
  end subroutine wrgnuplot_potentials
  
!######################################################################

  subroutine wrgnuplot_diabcp

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer             :: unit,s,i,k
    character(len=4)    :: am,am2,as1,as2
    character(len=220)  :: filename
    character(len=1200) :: string

!----------------------------------------------------------------------
! Filename
!----------------------------------------------------------------------
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    write(as1,'(i0)') dcpsta1
    write(as2,'(i0)') dcpsta2
    
    ! gnuplot file
    if (ldiagcut) then
       filename='plot_q'//trim(adjustl(am))//'_q'//trim(adjustl(am2)) &
            //'_s'//trim(adjustl(as1)) &
            //'_s'//trim(adjustl(as2)) &
            //'_diabcp.gnu'
    else
       filename='plot_q'//trim(adjustl(am)) &
            //'_s'//trim(adjustl(as1)) &
            //'_s'//trim(adjustl(as2)) &
            //'_diabcp.gnu'
    endif
       
!----------------------------------------------------------------------
! Open the gnuplot file
!----------------------------------------------------------------------
    call freeunit(unit)
    open(unit,file=filename,form='formatted',status='unknown')
    
!----------------------------------------------------------------------
! Energy ranges and states
!----------------------------------------------------------------------
    if (si.eq.-1) si=1
    if (sf.eq.-1) sf=nsta
    if (ei.eq.-999.0d0) ei=min(-0.1,0.98d0*minval(surf(:,si:sf)))
    if (ef.eq.-999.0d0) ef=1.02d0*maxval(surf(:,si:sf))

!----------------------------------------------------------------------
! Write the gnuplot file
!----------------------------------------------------------------------
    ! Set up
    write(unit,'(a)') 'set size square'
    write(unit,'(a)') 'unset key'
    write(unit,'(a)') 'monitorSize=system("xrandr | awk &
         ''/\*/{sub(/x/,\",\");print $1;exit}''")'
    !write(unit,'(a,/)') 'set terminal x11 size @monitorSize'

    ! Axis labels
    write(unit,'(a)') 'set ylabel ''Energy (eV)'''
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    string='set xlabel ''Q_{'//trim(adjustl(am))//'}'''
    if (ldiagcut) then
       k=len_trim(string)-1
       string=string(1:k)//' Q_{'//trim(adjustl(am2))//'}'''
    endif
    write(unit,'(a,/)') trim(string)
    
    ! Ranges
    write(unit,'(2(a,F6.2),a)') 'set xrange [',qi,':',qf,']'
    write(unit,'(2(a,F6.2),a,/)') 'set yrange [',ei,':',ef,']'

    ! eps output
    if (leps) then
       write(unit,'(/,a)') 'set terminal postscript eps &
            enhanced color solid "Helvetica" 20'
       write(unit,'(a)') 'set output '''&
            //filename(1:index(filename,'.gnu'))//'eps'''
    endif

    ! Plot command
    string='plot "-" w l lw 4 notitle, "-" w p pt 7 ps 1.5 lt 8 notitle'
    write(unit,'(a)') trim(string)

    ! Model diabatic coupling matrix element
    write(unit,'(/)')
    do i=1,npnts
       write(unit,*) qi+(i-1)*(qf-qi)/(npnts-1),surf(i,1)
    enddo
    write(unit,'(2x,a)') 'e'

    ! Ab inito data
    write(unit,*) 0.0d0,0.0d0
    do i=1,nabinit
       write(unit,*) qvec(mplt,iabinit(i)),abinit(dcpsta1,dcpsta2,i)
    enddo
    write(unit,'(2x,a)') 'e'

    ! pause -1
    if (.not.leps) write(unit,'(/,a)') 'pause -1'
    
!----------------------------------------------------------------------
! Open the gnuplot file
!----------------------------------------------------------------------
    call freeunit(unit)
    open(unit,file=filename,form='formatted',status='unknown')

!----------------------------------------------------------------------
! Close the gnuplot file
!----------------------------------------------------------------------
    close(unit)

!----------------------------------------------------------------------
! Plot the surfaces to the screen
!----------------------------------------------------------------------
    call system('gnuplot '//trim(filename))
    
    return
    
  end subroutine wrgnuplot_diabcp
  
!######################################################################

  subroutine wrgnuplot_dipoles

    use constants
    use iomod
    use sysinfo
    use pltglobal
    use parameters
    
    implicit none

    integer                        :: unit,i,c,k
    character(len=4)               :: am,am2,as1,as2
    character(len=220)             :: filename
    character(len=1200)            :: string
    character(len=1), dimension(3) :: acomp

    acomp=(/'x','y','z'/)
    
!----------------------------------------------------------------------
! Filename
!----------------------------------------------------------------------
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    write(as1,'(i0)') dipsta1
    write(as2,'(i0)') dipsta2
    
    ! gnuplot file
    if (ldiagcut) then
       filename='plot_q'//trim(adjustl(am))//'_q'//trim(adjustl(am2)) &
            //'_s'//trim(adjustl(as1)) &
            //'_s'//trim(adjustl(as2))//'_dip.gnu'
    else
       filename='plot_q'//trim(adjustl(am))//'_s'//trim(adjustl(as1)) &
            //'_s'//trim(adjustl(as2))//'_dip.gnu'
    endif
       
!----------------------------------------------------------------------
! Open the gnuplot file
!----------------------------------------------------------------------
    call freeunit(unit)
    open(unit,file=filename,form='formatted',status='unknown')

!----------------------------------------------------------------------
! Function ranges and states
!----------------------------------------------------------------------
    if (ei.eq.-999.0d0) ei=min(-0.1d0,0.98d0*minval(surf(:,1:3)))
    if (ef.eq.-999.0d0) ef=1.02d0*maxval(surf(:,1:3))

!----------------------------------------------------------------------
! Write the gnuplot file
!----------------------------------------------------------------------
    ! Set up
    write(unit,'(a)') 'set key spacing 1.5'
    write(unit,'(a)') 'set size square'
    write(unit,'(a)') 'monitorSize=system("xrandr | awk &
         ''/\*/{sub(/x/,\",\");print $1;exit}''")'
    !write(unit,'(a,/)') 'set terminal x11 size @monitorSize'

    ! Axis labels
    write(unit,'(a)') 'set ylabel ''D (a.u.)'''
    write(am,'(i0)') mplt
    write(am2,'(i0)') mplt2
    string='set xlabel ''Q_{'//trim(adjustl(am))//'}'''
    if (ldiagcut) then
       k=len_trim(string)-1
       string=string(1:k)//' Q_{'//trim(adjustl(am2))//'}'''
    endif
    write(unit,'(a,/)') trim(string)

    ! Ranges
    write(unit,'(2(a,F6.2),a)') 'set xrange [',qi,':',qf,']'
    write(unit,'(2(a,F6.2),a,/)') 'set yrange [',ei,':',ef,']'

    ! eps output
    if (leps) then
       write(unit,'(/,a)') 'set terminal postscript eps &
            enhanced color solid "Helvetica" 20'
       write(unit,'(a)') 'set output '''&
            //filename(1:index(filename,'.gnu'))//'eps'''
    endif

    ! Plot command
    string='plot '
    do c=1,3
       string=trim(string)//' "-" w l lw 4 title "'//acomp(c)//'",'
    enddo
     do c=1,3
       string=trim(string)//' "-" w p pt 7 ps 1.5 lt 8 notitle'
       if (c.ne.3) string=trim(string)//','
    enddo
    write(unit,'(a)') trim(string)

    ! Model dipole surfaces
    write(unit,'(/)')
    do c=1,3
       do i=1,npnts
          write(unit,*) qi+(i-1)*(qf-qi)/(npnts-1),surf(i,c)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo

    ! Ab inito data
    do c=1,3
       write(unit,*) 0.0d0,dip0(dipsta1,dipsta2,c)
       do i=1,nabinit
          write(unit,*) qvec(mplt,iabinit(i)),abinit(c,c,i)
       enddo
       write(unit,'(2x,a)') 'e'
    enddo

    ! pause -1
    if (.not.leps) write(unit,'(/,a)') 'pause -1'
    
!----------------------------------------------------------------------
! Close the gnuplot file
!----------------------------------------------------------------------
    close(unit)

!----------------------------------------------------------------------
! Plot the surfaces to the screen
!----------------------------------------------------------------------
    call system('gnuplot '//trim(filename))
    
    return
    
  end subroutine wrgnuplot_dipoles
    
!######################################################################
  
end program pltkdc
